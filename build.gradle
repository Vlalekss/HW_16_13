plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

allure {
    report {
        version.set('2.19.0')
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set('2.20.1') //версия интеграции фреймворка и Allure
            }
        }
    }
}

dependencies {
    testImplementation(
            'org.slf4j:slf4j-simple:1.7.30',
            'org.junit.jupiter:junit-jupiter:5.9.0',
            'com.github.javafaker:javafaker:1.0.2',
            'io.qameta.allure:allure-selenide:2.20.1',
            'com.codeborne:selenide:6.9.0'
    )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    systemProperties(System.getProperties())
    useJUnitPlatform()
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task RegistrationTests(type: Test) {
    useJUnitPlatform {
        includeTags("Registration")
    }
}